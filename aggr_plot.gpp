#!/usr/bin/env gnuplot
# This plots a each stream individually and all the frames it depends on, with the aggregate (sum or avg) plotted as well

set term pdfcairo enhanced font "Times,8" size 12,6
colors = "blue dark-green red dark-magenta dark-yellow orange dark-pink dark-cyan"
aggr_col = "black"

set for [i=0:words(colors)] style line 10+i pointtype 7 pointsize 0.25 linecolor rgb word(colors,i+1)
set style line 1 pointtype 7 pointsize 0.25 linecolor aggr_col
set key Left
if (!exists("ver"))     ver     = 3                                                            # Which encode configuration version
if (!exists("depfile")) depfile = "deps"                                                       # Dependency configuration file
if (!exists("start"))   start   = system(sprintf("head -1 %s | cut -d ':' -f 1", depfile)) + 0 # Starting stream is user-specified or the first in the dependency file
if (!exists("end"))     end     = system(sprintf("tail -1 %s | cut -d ':' -f 1", depfile)) + 0 # Final stream is user-specified or the last in the dependency file


# Frame vs Size
if(exists("title")) set title title.': Frame vs Size'
set xlabel "Frame"
set ylabel "Size (bits)"
set xrange [-1:201]
set xtics 0,8,200
set yrange [0:2000000]
set grid xtics linetype 1 linewidth 0.25 linecolor rgb "#cccccc"
set output ''.ver.'/aggr_frame_v_size.pdf'
do for [i=start:end] {
  deps=system(sprintf("grep -e ^%d %s | cut -d ':' -f 2",i, depfile))  # Get dependencies of stream i
  set multiplot
    set key at 200,1970000
    plot ''.ver.'/stream'.i.'.dat' using 2:4 with points linestyle 10+i title 'Stream '.i
    plot ''.ver.'/stream'.i.'.dat' using 2:4:1 with labels offset 1 textcolor rgb word(colors,i+1) notitle
    if(deps != "") {
      set key at 200,1940000
      plot for [dep in deps] sprintf("<(grep REF_VIEW %s)", ''.ver.'/stream'.dep.'.dat') using 2:4 with points linestyle 10+dep title 'Stream '.dep
      plot for [dep in deps] sprintf("<(grep REF_VIEW %s)", ''.ver.'/stream'.dep.'.dat') using 2:4:1 with labels offset 1 textcolor rgb word(colors,dep+1) notitle
      set key at 200,2000000
      plot sprintf("<for i in %s; do grep REF_VIEW %d/stream${i}.dat; done | cat - %d/stream%d.dat | grep -v Frame | awk '{array[$2]+=$4; if($5==\"REF_VIEW\") {isref[$2]=$5}} END { for (i in array) {printf \"%-10s %-10d %-10s\\n\",i,array[i],isref[i] }}' | grep REF_VIEW | sort -nk 2",deps,ver,ver,i) using 1:2 with points linestyle 1 title 'Total'
    }
  unset multiplot
}

# Total size of each stream, with and without dependencies
if(exists("title")) set title title.': Total size of each stream, with and without dependencies'
set xrange [-1:8]
set yrange[0:60000000]
set boxwidth 0.25
set style fill solid
set xtics 0,1,7
unset grid
set key at 8,60000000
plot sprintf("<for stream in \`seq %d %d \`; do for i in \`grep -e ^\$stream %s | cut -d ':' -f 2\`; do grep REF_VIEW %d/stream${i}.dat; done | cat - %d/stream${stream}.dat | grep -v Frame | awk '{sum+=$4} END { print '$stream'\"  \"sum; }'; done",start,end,depfile,ver,ver) using ($1):2 with boxes linecolor rgb "blue" title "Total Size with Dependent Frames" ,\
     sprintf("<for stream in \`seq %d %d \`; do grep -v Frame %d/stream${stream}.dat | awk '{sum+=$4} END { print '$stream'\"  \"sum; }'; done",start,end,ver) using ($1-0.3):2 with boxes linecolor rgb "red" title "Total Size of Individual Frame", \
     sprintf("<for stream in \`seq %d %d \`; do for i in \`grep -e ^\$stream %s | cut -d ':' -f 2\`; do cat %d/stream${i}.dat; done | cat - %d/stream${stream}.dat | grep -v Frame | awk '{sum+=$4} END { print '$stream'\"  \"sum; }'; done",start,end,depfile,ver,ver) using ($1+0.3):2 with boxes linecolor rgb "black" title "Total Size with All Frames in Dependent Stream"
set output

# Frame vs PSNR
if(exists("title")) set title title.': Frame vs PSNR' 
set output ''.ver.'/aggr_frame_v_psnr.pdf'
set ylabel "PSNR (dB)"
set xrange [-1:201]
set xtics 0,8,200
set yrange [28:34]
set ytics 28,0.5,34
do for [i=start:end] {
  deps=system(sprintf("grep -e ^%d %s | cut -d ':' -f 2",i, depfile))  # Get dependencies of stream i
  set multiplot
    set key at 200,33.8
    plot ''.ver.'/stream'.i.'.dat' using 2:3 with points linestyle 10+i title 'Stream '.i
    plot ''.ver.'/stream'.i.'.dat' using 2:3:1 with labels offset 1 textcolor rgb word(colors,i+1) notitle
    set key at 200,33.7
    if(deps != "") {
      plot for [dep in deps] sprintf("<(grep REF_VIEW %s)", ''.ver.'/stream'.dep.'.dat') using 2:3 with points linestyle 10+dep title 'Stream '.dep
      plot for [dep in deps] sprintf("<(grep REF_VIEW %s)", ''.ver.'/stream'.dep.'.dat') using 2:3:1 with labels offset 1 textcolor rgb word(colors,dep+1) notitle
      set key at 200,33.9
      plot sprintf("<for i in %s; do grep REF_VIEW %d/stream${i}.dat; done | cat - %d/stream%d.dat | grep -v Frame | awk '{array[$2]+=$3; ++count[$2]; if($5==\"REF_VIEW\") {isref[$2]=$5}} END { for (i in array) {printf \"%-10s %-10f %10s\\n\",i,array[i]/count[i],isref[i] }}' | grep REF_VIEW | sort -nk 2",deps,ver,ver,i) using 1:2 with points linestyle 1 title 'Average'
    }
  unset multiplot
}

# Average PSNR of all streams, with and without dependencies
if(exists("title")) set title title.': Average PSNR of all streams, with and without dependencies'
set xrange [-1:8]
set yrange [28:34]
set boxwidth 0.25
set style fill solid
set xtics 0,1,7
unset grid
set key at 7,33.8
plot sprintf("<for stream in \`seq %d %d \`; do for i in \`grep -e ^\$stream %s | cut -d ':' -f 2\`; do grep REF_VIEW %d/stream${i}.dat; done | cat - %d/stream${stream}.dat | grep -v Frame | awk '{sum+=$3;count+=1} END { print '$stream'\"  \"sum/count; }'; done",start,end,depfile,ver,ver) using ($1):2 with boxes linecolor rgb "blue" title "Average PSNR with Dependent Frames" ,\
     sprintf("<for stream in \`seq %d %d \`; do grep -v Frame %d/stream${stream}.dat | awk '{sum+=$3;count+=1} END { print '$stream'\"  \"sum/count; }'; done",start,end,ver) using ($1-0.3):2 with boxes linecolor rgb "red" title "Average PSNR of Individual Frames",\
     sprintf("<for stream in \`seq %d %d \`; do for i in \`grep -e ^\$stream %s | cut -d ':' -f 2\`; do cat %d/stream${i}.dat; done | cat - %d/stream${stream}.dat | grep -v Frame | awk '{sum+=$3;count+=1} END { print '$stream'\"  \"sum/count; }'; done",start,end,depfile,ver,ver) using ($1+0.3):2 with boxes linecolor rgb "black" title "Average PSNR with All Frames in Dependent Stream" ,\

set output
